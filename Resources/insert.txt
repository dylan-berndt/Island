uniform mat4 projection;
uniform mat4 model;
uniform mat4 view;
uniform vec3 camera;

uniform vec3 lightDirection;
uniform vec3 lightColor = vec3(1.0);

uniform int width;
uniform int height;

uniform float time;

struct Texture {
    sampler2D id;
    vec3 offset;
    vec3 scale;
    float bumpMultiplier;
};

uniform float near = 0.1;
uniform float far = 500.0;

uniform sampler2DArray shadowMap;
layout (std140) uniform LightSpaceMatrices
{
    mat4 lightSpaceMatrices[16];
};
uniform float cascadePlaneDistances[16];
uniform int cascadeCount;

int getShadowLayer(vec3 pos) {
    vec4 fragPosViewSpace = view * vec4(pos, 1.0);
    float depthValue = abs(fragPosViewSpace.z);

    int layer = -1;
    for (int i = 0; i < cascadeCount; ++i)
    {
        if (depthValue < cascadePlaneDistances[i])
        {
            layer = i;
            break;
        }
    }
    if (layer == -1)
    {
        layer = cascadeCount;
    }
    return layer;
}

float shadow(vec3 pos, int shadowKernel) {
    int layer = getShadowLayer(pos);

    vec4 position = lightSpaceMatrices[layer] * vec4(pos, 1.0);
    vec3 projCoords = position.xyz / position.w;

    projCoords = projCoords * 0.5 + 0.5;

    float currentDepth = projCoords.z;

    if (currentDepth > 1.0) {
        return 0.0;
    }

    float bias = 0.00005;
    const float biasModifier = 0.5f;
    if (layer == cascadeCount)
    {
        bias *= 1 / (far * biasModifier);
    }
    else
    {
        bias *= 1 / (cascadePlaneDistances[layer] * biasModifier);
    }

    float shadow = 0.0;
    vec2 texelSize = 1.0 / vec2(textureSize(shadowMap, 0));

    for(int x = -shadowKernel / 2; x <= shadowKernel / 2; ++x)
    {
        for(int y = -shadowKernel / 2; y <= shadowKernel / 2; ++y)
        {
            float pcfDepth = texture(shadowMap, vec3(projCoords.xy + vec2(x, y) * texelSize, layer)).r;
            shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;
        }
    }
    shadow /= float(shadowKernel * shadowKernel);

    if(projCoords.z > 1.0 || projCoords.z < 0.0) {
        shadow = 0.0;
    }

    return shadow;
}

float linearDepth(float depth)
{
    float z = depth * 2.0 - 1.0;
    return (2.0 * near * far) / (far + near - z * (far - near));
}

vec3 worldPosition(vec2 uv, float depth) {
    vec4 boxPos = vec4(uv * 2.0 - 1.0, depth * 2.0 - 1.0, 1.0);
    vec4 pos = inverse(projection * view) * boxPos;
    return pos.xyz / pos.w;
}

